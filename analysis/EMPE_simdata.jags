
    model {

    ###############################
    # Process model
    ###############################
    
    # Occupancy state in each year, at each colony
    for (s in 1:n_sites){
      for (t in 1:n_years){
        z_occ[s,t] ~ dbern(prob_occ)
      }
    }
    
    # Population growth process
    r_tau <- pow(r_sd,-2)
    
    for (s in 1:n_sites){
    
      log_X[s,1] <- log(N0[s])
      N[s,1] <- exp(log_X[s,1]) * z_occ[s,1]

      for (t in 1:(n_years-1)){
      
        log_X[s,t+1] ~ dnorm(log_X[s,t] + r_mean[s] + 0.5 * r_sd * r_sd, r_tau)
        N[s,t+1] <- exp(log_X[s,t+1]) * z_occ[s,t+1]
        
      }
      
    } 

    ###############################
    # Generate aerial observations
    ###############################
    
    aerial_tau <- pow(aerial_sigma,-2)
   
    for (i in 1:n_obs_aerial){
    
      # Adult counts assumed to be poisson distributed with mean centred on seasonal expected value
      log_lambda[i] ~ dnorm(log_X[aerial_site[i],aerial_year[i]] - 0.5 * aerial_sigma * aerial_sigma, aerial_tau)
      adult_count[i] ~ dpois(z_occ[aerial_site[i],aerial_year[i]] * exp(log_lambda[i]))
      
      adult_count_sim[i] ~ dpois(z_occ[aerial_site[i],aerial_year[i]] * exp(log_lambda[i]))
    }
    
    ###############################
    # Generate satellite observations
    ###############################
    
    for (i in 1:n_obs_satellite){
    
      # Observation error (and bias) for satellite counts is estimated from data
      sat_mean[i] <- N[satellite_site[i],satellite_year[i]] * sat_slope 
      sat_sd[i] <- sat_mean[i] * sat_CV + 0.001
      sat_tau[i] <- pow(sat_sd[i],-2)
      
      sat_z[i] ~ dbern(sat_p)
      satellite[i] ~ dnorm(sat_mean[i] * sat_z[i],sat_tau[i])
      
      satellite_sim[i] ~ dnorm(sat_mean[i] * sat_z[i],sat_tau[i])
    }
    
    ###############################
    # Derived quantities
    ###############################
    
    # Global abundance each year
    for (t in 1:n_years){
      N_global[t] <- sum(N[1:n_sites,t])
    }
    
    # Global trend
    global_trend <- inprod(log(N_global[1:n_years]),regression_weights[1,1:n_years])

    }
    
